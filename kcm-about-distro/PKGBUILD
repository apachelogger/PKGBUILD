# Maintainer: Leszek Lesner <leszek@zevenos.com>
# addapted from birdflesh <antkoul at gmail dot com> AUR pkgbuild

pkgname=kcm-about-distro
_pkgname=about-distro
pkgver=1.0.1
pkgrel=1
pkgdesc="KCM displaying distribution and system information"
arch=('i686' 'x86_64')
url='https://projects.kde.org/about-distro'
license=('GPL3')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/$_pkgname/$pkgver/src/$_pkgname-$pkgver.tar.xz"
        "kcm-about-distrorc"
        "netrunner-logo.png")
md5sums=('fc8c0dddb94ffe148d5f2fd78e865a98'
         'f2cd5b00b092866b71dacb434cdb14f1'
         'b0557512af12bee952303212a9747860')

build() {
  cd "$srcdir"
  
  # Remove release date, taken from msx's PKGBUILD on ccr
  sed -i 's/%1\ %2/%1/g' $_pkgname-$pkgver/src/Module.cpp
  
  mkdir build
  cd build
  cmake ../$_pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/kcm-about-distrorc" "$pkgdir/usr/share/config/kcm-about-distrorc"
  install -Dm644 "$srcdir/netrunner-logo.png" "$pkgdir/usr/share/apps/$_pkgname/netrunner-logo.png"
}
